# source
# https://mothur.org/wiki/batch_mode/

##################
##### Names and labels
# MOTHUR Project working folders
proj_wd=./

system(mkdir $proj_wd/res_out)
out_dir=$proj_wd/res_out

system(mkdir $proj_wd/log_out)
log_out=$proj_wd/log_out

# set input folder with fastq files
fq_in=$proj_wd/cut_out


# Project name
# Prefix for the output files
proj_name=MATE_MothurProject

#logfile name for this analysis
log_name=MATE_16SV3V4_project

# set logfile location
set.logfile(name=$log_out/$log_name)


# oligos=$proj_wd/oligos.txt

# Variables for MOTHUR functions

#set number of processors to use during analysis
proc=6

#set type of data fastq or gz
fqtype=gz
# make.contigs
make_contigs_deltaq=10
# screen.seq 01 after make.contigs
screen_seq_01_minl=400
screen_seq_01_maxl=500
screen_seq_01_maxamb=0
screen_seq_01_maxhomop=8

# screen.seq 02 after align.seq
screen_seq_02_start=1
screen_seq_02_end=18929

# pcr.seq start end positions
# For silva.nr_v138_2.align and silva.seed_v138_2.align
# 16S ILMN 314F-805R WITH PRIMERS
# 6388 25316
# 16S ILMN 314F-805R WITHOUT PRIMERS
# 6426    23443
pcr_seq_start=6428    
pcr_seq_end=23440

# pre.cluster diff
preCdiff=2

# split.abund(cutoff=1 can be set???

# classify_seqs_cutoff
classify_seqs_cutoff=80

# contaminents to remove from data in remove.linage
contaminants=Chloroplast-Mitochondria-unknown-Archaea-Eukaryota

# dist_seqs_cutoff
dist_seqs_cutoff=0.15

# cluster
cluster_cutoff=0.15
cluster_method=average

# get.oturep
get_oturep_cutoff=0.03
get_oturep_method=abundance

# rarefaction.single
rarefaction_single_calc=sobs
rarefaction_single_freq=100


# Reference source
# https://mothur.org/wiki/silva_reference_files/

# Set location of lab reference files
# REFERENCE_LOCATION=/Users/sarahwestcott/Desktop/release
reference_dir=/mnt/00F68F456EE8961F/BioInfo/00_References/Metagenom/Amplicon/MOTHUR

##################
# MOTHUR REFRENCE FILES
# https://mothur.org/wiki/silva_reference_files/



##################
# PRIMARY ALIGNMENT 

###########################
# REFERENCES USED IN SCRIPT
#alignref=/mnt/00F68F456EE8961F/BioInfo/00_References/Metagenom/Amplicon/MOTHUR/Full/V138.2/silva.nr_v138_2.align
#taxonref=/mnt/00F68F456EE8961F/BioInfo/00_References/Metagenom/Amplicon/MOTHUR/Full/V138.2/silva.nr_v138_2.tax
#alignref=/home/gyula/bfx_sources/RefSeqs/Amplicon/Silva/16S/MOTHUR/Full/V138.2/silva.nr_v138_2.align
alignref=/home/gyula/bfx_sources/RefSeqs/Amplicon/Silva/16S/MOTHUR/Full/V138.2/silva.seed_v138_2.align
#taxonref=/home/gyula/bfx_sources/RefSeqs/Amplicon/Silva/16S/MOTHUR/Full/V138.2/silva.nr_v138_2.tax
taxonref=/home/gyula/bfx_sources/RefSeqs/Amplicon/Silva/16S/MOTHUR/Full/V138.2/silva.seed_v138_2.tax





#run generic Standard Operating Procedure
# ./mothur_pipeline_MATE_current_HP-DL-G10_V0.2_gyula-P50.batch
# commands from MOTHUR MiSeq_SOP
# https://mothur.org/wiki/miseq_sop/
#
#


system(echo "New analysis started  at $(date)")
system(rm $proj_wd/res_out/*)

#  quit

make.file(inputdir=$fq_in, outputdir=$proj_wd/res_out, type=$fqtype, prefix=$proj_name)
# PAR deltaq=10
make.contigs(outputdir=$proj_wd/res_out, inputdir=$fq_in, file=current, processors=$proc, deltaq=$make_contigs_deltaq)

set.dir(input=$proj_wd/res_out, output=$proj_wd/res_out)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs after make contigs")

screen.seqs(fasta=current, count=current, minlength=$screen_seq_01_minl, maxlength=$screen_seq_01_maxl, maxhomop=$screen_seq_01_maxhomop, maxambig=$screen_seq_01_maxamb)

system(echo "screen.seqs after make contigs")

# get.current()

summary.seqs(fasta=current, count=current)

unique.seqs(fasta=current, count=current)

summary.seqs(count=current)

system(echo "summary.seqs after unique.seqs")

count.groups(count=current)

system(echo "count.groups after summary.seqs")

# is it neccessary?
# count.groups(count=current)
# redundant removed
# screen.seqs(fasta=current, count=current, maxambig=0)

# redundant removed
# summary.seqs(count=current)

# redundant removed
# system(echo "summary.seqs after screen.seqs maxamb=0")

sub.sample(fasta=current, count=current, persample=T)

# necessary??
count.groups(count=current)

system(echo "count.groups after sub.sample")
# sub.sample(fasta=current, count=current, persample=T)
# # necessary??
# count.groups(count=current)
# system(echo "count.groups after sub.sample")

align.seqs(fasta=current, reference=$alignref)

summary.seqs(fasta=current, count=current)

# PAR
# start=$screen_seq_02_minl end=$screen_seq_02_maxl 
screen.seqs(fasta=current, count=current, start=$pcr_seq_start, end=$pcr_seq_end)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs align.seqs and screen.seqs")

filter.seqs(fasta=current, trump=.)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs filter.seqs")

unique.seqs(fasta=current, count=current)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs unique.seqs")

pre.cluster(fasta=current, count=current, diffs=$preCdiff)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs pre.cluster")

chimera.uchime(fasta=current, count=current, dereplicate=T)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs chimera.uchime")

split.abund(cutoff=1, fasta=current, count=current)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs split rare")

set.current(fasta=MATE_MothurProject.trim.contigs.good.unique.subsample.good.filter.unique.precluster.denovo.uchime.abund.fasta, count=MATE_MothurProject.trim.contigs.good.unique.subsample.good.filter.unique.precluster.denovo.uchime.abund.count_table)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs split abund")

# PAR
# classify_seqs_cutoff
classify.seqs(fasta=current, count=current, method=wang, reference=$alignref, taxonomy=$taxonref, cutoff=$classify_seqs_cutoff, output=simple)

system(echo "simple taxa-sample list")

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs classify.seqs")

remove.lineage(fasta=current, count=current, taxonomy=current, taxon=Archaea-Chloroplast-Mitochondria-Eukaryota-unknown)

summary.seqs(fasta=current, count=current)

system(echo "summary.seqs remove.lineage")

summary.tax(taxonomy=current, count=current, output=detail)


# summary.tax(taxonomy=current, count=current)

# PAR
# dist_seqs_cutoff
dist.seqs(fasta=current, cutoff=$dist_seqs_cutoff)

# PAR
# cluster_cutoff
# cluster_method
cluster(column=current, count=current, cutoff=$cluster_cutoff, method=$cluster_method)

make.shared(list=current, label=0.03, count=current)

count.groups(shared=current)

classify.otu(list=current, taxonomy=current, label=0.03, count=current, basis=sequence, relabund=T)

system(cp res_out/*.an.0.03.cons.tax.summary res_out/${path##*/}_taxonomy_table.summary)
system(cp res_out/*.an.0.03.cons.taxonomy res_out/${path##*/}_taxonomy_list.taxonomy)

# create krona chart
system(python ./mothur_krona_XML_gy.py res_out/${path##*/}_taxonomy_table.summary > res_out/${PWD##*/}_taxonomy_table.summary.xml)
system(ktImportXML -o res_out/${PWD##*/}_taxonomy_table.summary.html res_out/${path##*/}_taxonomy_table.summary.xml)

# PAR
# get_oturep_cutoff
# get_oturep_method
get.oturep(list=current, fasta=current, count=current, method=$get_oturep_method, cutoff=$get_oturep_cutoff)

degap.seqs(fasta=current)

summary.single(shared=current, subsample=T, calc=nseqs-coverage-sobs-chao-ace-shannon-invsimpson)

system(cp res_out/*.an.groups.ave-std.summary res_out/${path##*/}_ASV_diversity_data.summary)

sub.sample(shared=current)

system(cp res_out/*.an.0.03.subsample.shared res_out/${path##*/}_ASV_distribution.shared)

# PAR
# rarefaction_single_calc
# rarefaction_single_freq
rarefaction.single(shared=current, calc=$rarefaction_single_calc, freq=$rarefaction_single_freq)





system(echo "New analysis finished at $(date)")

quit()


